;~ #AutoIt3Wrapper_UseX64=y
#AutoIt3Wrapper_UseX64=n
#AutoIt3Wrapper_Run_AU3Check=Y
#AutoIt3Wrapper_AU3Check_Stop_OnWarning=y
#AutoIt3Wrapper_AU3Check_Parameters=-d -w 1 -w 2 -w 3 -w 4 -w 5 -w 6 -w 7
#Au3Stripper_Ignore_Funcs=__NetWebView2_WebViewEvents__*,__NetWebView2_JSEvents__*

#include <Array.au3>
#include <GUIConstantsEx.au3>
#include <WinAPIShPath.au3>
#include <WindowsConstants.au3>

#TODO UDF HEADER
#TODO UDF INDEX

; Global objects
Global $_g_hNetWebView2Lib_DLL = ''
Global $_g_oWeb
Global $g_DebugInfo = True
Global $_g_sUser_FnPrefix__NetWebView2_JSEvents = ""
Global $_g_sUser_FnPrefix__NetWebView2_WebViewEvents = ""

#Region ; NetWebView2Lib UDF - core function
Func _NetWebView2_StartUp($sDLLFileFullPath)
	#RegistrationFree is WorkInProgress
	$sDLLFileFullPath = _WinAPI_PathCanonicalize($sDLLFileFullPath)
;~ 	ConsoleWrite($sDLLFileFullPath & @CRLF)
	$_g_hNetWebView2Lib_DLL = DllOpen($sDLLFileFullPath)

;~ 	ConsoleWrite("! " & VarGetType($_g_hNetWebView2Lib_DLL) & @CRLF)
;~ 	ConsoleWrite("! " & $_g_hNetWebView2Lib_DLL & @CRLF)
	If $_g_hNetWebView2Lib_DLL = -1 Then
		__NetWebView2_Log(@ScriptLineNumber, 'Error loading AcitevX DLL : ' & $_g_hNetWebView2Lib_DLL)
		Return SetError(1, @extended, $_g_hNetWebView2Lib_DLL)
	EndIf
	Return SetError(@error, @extended, $_g_hNetWebView2Lib_DLL)
EndFunc   ;==>_NetWebView2_StartUp

Func _NetWebView2_ShutDown()
	#RegistrationFree is WorkInProgress
	DllClose($_g_hNetWebView2Lib_DLL)
EndFunc   ;==>_NetWebView2_ShutDown

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_Initialize
; Description ...:
; Syntax ........: _NetWebView2_Initialize(ByRef $oWebV2M, $hGUI, $sProfileDirectory[, $i_Left = 0[, $i_Top = 0[, $i_Width = 0[,
;                  $i_Height = 0[, $b_LoadWait = True[, $b_SetAutoResize = True[, $b_AreDevToolsEnabled = True[,
;                  $i_ZoomFactor = 1.0[, $s_BackColor = "0x2B2B2B"]]]]]]]]])
; Parameters ....: $oWebV2M             - [in/out] an object.
;                  $hGUI                - a handle value.
;                  $sProfileDirectory   - a string value.
;                  $i_Left              - [optional] an integer value. Default is 0.
;                  $i_Top               - [optional] an integer value. Default is 0.
;                  $i_Width             - [optional] an integer value. Default is 0.
;                  $i_Height            - [optional] an integer value. Default is 0.
;                  $b_LoadWait          - [optional] a boolean value. Default is True.
;                  $b_SetAutoResize     - [optional] a boolean value. Default is True.
;                  $b_AreDevToolsEnabled- [optional] a boolean value. Default is True.
;                  $i_ZoomFactor        - [optional] an integer value. Default is 1.0.
;                  $s_BackColor         - [optional] a string value. Default is "0x2B2B2B".
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_Initialize(ByRef $oWebV2M, $hGUI, $sProfileDirectory, $i_Left = 0, $i_Top = 0, $i_Width = 0, $i_Height = 0, $b_LoadWait = True, $b_SetAutoResize = True, $b_AreDevToolsEnabled = True, $i_ZoomFactor = 1.0, $s_BackColor = "0x2B2B2B")

	; Important: Pass $hGUI in parentheses to maintain Pointer type for COM
	Local $iInit = $oWebV2M.Initialize(($hGUI), $sProfileDirectory, $i_Left, $i_Top, $i_Width, $i_Height)
	If @error Then Return SetError(@error, @extended, $iInit)

	If $b_LoadWait Then _NetWebView2_LoadWait($oWebV2M)
	If @error Then Return SetError(@error, @extended, $iInit)

	; WebView2 Configuration
	$oWebV2M.SetAutoResize($b_SetAutoResize) ; Using SetAutoResize(True) to skip WM_SIZE
	$oWebV2M.AreDevToolsEnabled = $b_AreDevToolsEnabled ; Allow F12
	$oWebV2M.ZoomFactor = $i_ZoomFactor
	$oWebV2M.BackColor = $s_BackColor
	Return SetError(@error, @extended, '')
EndFunc   ;==>_NetWebView2_Initialize

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_CreateManager
; Description ...:
; Syntax ........: _NetWebView2_CreateManager([$sUser_FnPrefix = ""])
; Parameters ....: $sUser_FnPrefix           - [optional] a string value. Default is "".
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_CreateManager($sUser_FnPrefix = "")
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError
	Local $oWebV2M = ObjCreate("NetWebView2.Manager") ; REGISTERED VERSION
;~ 	__NetWebView2_ObjName_FlagsValue($oWebV2M)
	If @error Then __NetWebView2_Log(@ScriptLineNumber, "! [NetWebView2Lib]: Manager Creation ERROR")

	If $sUser_FnPrefix Then $_g_sUser_FnPrefix__NetWebView2_WebViewEvents = $sUser_FnPrefix
	ObjEvent($oWebV2M, "__NetWebView2_WebViewEvents__", "IWebViewEvents")
	Return SetError(@error, @extended, $oWebV2M)
EndFunc   ;==>_NetWebView2_CreateManager

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_GetBridge
; Description ...:
; Syntax ........: _NetWebView2_GetBridge(ByRef $oWebV2M[, $sUser_FnPrefix = ""])
; Parameters ....: $oWebV2M             - [in/out] an object.
;                  $sUser_FnPrefix           - [optional] a string value. Default is "".
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_GetBridge(ByRef $oWebV2M, $sUser_FnPrefix = "")
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	Local $oWebJS = $oWebV2M.GetBridge()
	If @error Then __NetWebView2_Log(@ScriptLineNumber, "! [NetWebView2Lib]: Manager.GetBridge() ERROR")

	If $sUser_FnPrefix Then $_g_sUser_FnPrefix__NetWebView2_JSEvents = $sUser_FnPrefix
	ObjEvent($oWebJS, "__NetWebView2_JSEvents__", "IBridgeEvents")
	Return SetError(@error, @extended, $oWebJS)
EndFunc   ;==>_NetWebView2_GetBridge

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetJson_CreateParser
; Description ...:
; Syntax ........: _NetJson_CreateParser([$sInitialJson = "{}"])
; Parameters ....: $sInitialJson        - [optional] a string value. Default is "{}".
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetJson_CreateParser($sInitialJson = "{}")
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError
	If $sInitialJson = "" Or $sInitialJson = Default Then $sInitialJson = "{}"
	Local $oParser = ObjCreate("NetJson.Parser") ; REGISTERED VERSION
	If Not IsObj($oParser) Then Return SetError(1, 0, 0)
	$oParser.Parse($sInitialJson)
	Return $oParser
EndFunc   ;==>_NetJson_CreateParser

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_ExecuteScript
; Description ...:
; Syntax ........: _NetWebView2_ExecuteScript(ByRef $oWebV2M, $sJavaScript)
; Parameters ....: $oWebV2M             - [in/out] an object.
;                  $sJavaScript         - a string value.
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_ExecuteScript(ByRef $oWebV2M, $sJavaScript)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	Local $iRet = $oWebV2M.ExecuteScript($sJavaScript)
	Return SetError(@error, @extended, $iRet)
EndFunc   ;==>_NetWebView2_ExecuteScript

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_CleanUp
; Description ...:
; Syntax ........: _NetWebView2_CleanUp(ByRef $oWebV2M, ByRef $oJSBridge)
; Parameters ....: $oWebV2M             - [in/out] an object.
;                  $oJSBridge           - [in/out] an object.
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_CleanUp(ByRef $oWebV2M, ByRef $oJSBridge)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	Local $iRet = $oWebV2M.Cleanup()
	If @error Then Return SetError(@error, @extended, $iRet)

	$oWebV2M = 0
	$oJSBridge = 0
	Return SetError(@error, @extended, $iRet)
EndFunc   ;==>_NetWebView2_CleanUp

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_LoadWait
; Description ...:
; Syntax ........: _NetWebView2_LoadWait(ByRef $oWebV2M)
; Parameters ....: $oWebV2M             - [in/out] an object.
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_LoadWait(ByRef $oWebV2M)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	; Wait for WebView2 to be ready
	While Sleep(10)
		If $oWebV2M.IsReady Then ExitLoop
		If @error Then Return SetError(@error, @extended, -1)
	WEnd

EndFunc   ;==>_NetWebView2_LoadWait

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_Navigate
; Description ...:
; Syntax ........: _NetWebView2_Navigate(ByRef $oWebV2M, $sURL[, $b_LoadWait = True])
; Parameters ....: $oWebV2M             - [in/out] an object.
;                  $sURL                - a string value.
;                  $b_LoadWait          - [optional] a boolean value. Default is True.
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_Navigate(ByRef $oWebV2M, $sURL, $b_LoadWait = True)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	Local $iNavigation = $oWebV2M.Navigate($sURL)
	If @error Then Return SetError(@error, @extended, $iNavigation)

	If $b_LoadWait Then _NetWebView2_LoadWait($oWebV2M)
	Return SetError(@error, @extended, '')
EndFunc   ;==>_NetWebView2_Navigate

; #FUNCTION# ====================================================================================================================
; Name ..........: _NetWebView2_NavigateToString
; Description ...:
; Syntax ........: _NetWebView2_NavigateToString(ByRef $oWebV2M, $s_HTML[, $b_LoadWait = True])
; Parameters ....: $oWebV2M             - [in/out] an object.
;                  $s_HTML              - a string value.
;                  $b_LoadWait          - [optional] a boolean value. Default is True.
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func _NetWebView2_NavigateToString(ByRef $oWebV2M, $s_HTML, $b_LoadWait = True)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	Local $iNavigation = $oWebV2M.NavigateToString($s_HTML)
	If @error Then Return SetError(@error, @extended, $iNavigation)

	If $b_LoadWait Then _NetWebView2_LoadWait($oWebV2M)
	Return SetError(@error, @extended, '')
EndFunc   ;==>_NetWebView2_NavigateToString
#EndRegion ; NetWebView2Lib UDF - core function

#Region ; NetWebView2Lib UDF - helper function
#EndRegion ; NetWebView2Lib UDF - helper function

#Region ; NetWebView2Lib UDF - #INTERNAL_USE_ONLY#
; #INTERNAL_USE_ONLY# ===========================================================================================================
; Name ..........: __NetWebView2_Log
; Description ...: Debug Write utility
; Syntax ........: __NetWebView2_Log($s_ScriptLineNumber, $sString[, $iErrorNoLineNo = 1[, $iError = @error[, $iExtended = @extended]]])
; Parameters ....: $s_ScriptLineNumber  - a string value.
;                  $sString             - a string value.
;                  $iErrorNoLineNo      - [optional] an integer value. Default is 1.
;                  $iError              - [optional] an integer value. Default is @error.
;                  $iExtended           - [optional] an integer value. Default is @extended.
; Return values .: None
; Author ........: mLipok, ioa747
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func __NetWebView2_Log($s_ScriptLineNumber, $sString, $iErrorNoLineNo = 1, $iError = @error, $iExtended = @extended)
	If Not $g_DebugInfo Then Return SetError($iError, $iExtended, 0)
	If $iErrorNoLineNo = 1 Then
		If $iError Then
			$sString = "@@ ( NetWebView2Lib UDF : Line=" & $s_ScriptLineNumber & ", @error=" & $iError & ", @extended=" & $iExtended & " ) :: " & $sString
		Else
			$sString = "+> ( NetWebView2Lib UDF : Line=" & $s_ScriptLineNumber & " ) :: " & $sString
		EndIf
	EndIf
	Local $iReturn = ConsoleWrite($sString & @CRLF)
	Return SetError($iError, $iExtended, $iReturn)
EndFunc   ;==>__NetWebView2_Log

; #INTERNAL_USE_ONLY# ===========================================================================================================
; Name ..........: __NetWebView2_ObjName_FlagsValue
; Description ...:
; Syntax ........: __NetWebView2_ObjName_FlagsValue(ByRef $oObj)
; Parameters ....: $oObj                - [in/out] an object.
; Return values .: None
; Author ........: AutoIt HelpFile ObjName Example 2
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func __NetWebView2_ObjName_FlagsValue(ByRef $oObj)
	Local $sInfo = ''
	$sInfo &= '+>' & @TAB & 'ObjName($oObj,1) {The name of the Object} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_NAME) & @CRLF

	; HELPFILE REMARKS: Not all Objects support flags 2 to 7. Always test for @error in these cases.
	$sInfo &= '+>' & @TAB & 'ObjName($oObj,2) {Description string of the Object} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_STRING)
	If @error Then $sInfo &= '@error = ' & @error
	$sInfo &= @CRLF & @CRLF

	$sInfo &= '+>' & @TAB & 'ObjName($oObj,3) {The ProgID of the Object} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_PROGID)
	If @error Then $sInfo &= '@error = ' & @error
	$sInfo &= @CRLF & @CRLF

	$sInfo &= '+>' & @TAB & 'ObjName($oObj,4) {The file that is associated with the object in the Registry} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_FILE)
	If @error Then $sInfo &= '@error = ' & @error
	$sInfo &= @CRLF & @CRLF

	$sInfo &= '+>' & @TAB & 'ObjName($oObj,5) {Module name in which the object runs (WIN XP And above). Marshaller for non-inproc objects.} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_MODULE)
	If @error Then $sInfo &= '@error = ' & @error
	$sInfo &= @CRLF & @CRLF

	$sInfo &= '+>' & @TAB & 'ObjName($oObj,6) {CLSID of the object''s coclass} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_CLSID)
	If @error Then $sInfo &= '@error = ' & @error
	$sInfo &= @CRLF & @CRLF

	$sInfo &= '+>' & @TAB & 'ObjName($oObj,7) {IID of the object''s interface} =' & @CRLF & @TAB & ObjName($oObj, $OBJ_IID)
	If @error Then $sInfo &= '@error = ' & @error
	$sInfo &= @CRLF & @CRLF

	ConsoleWrite($sInfo & @CRLF)
EndFunc   ;==>__NetWebView2_ObjName_FlagsValue
#EndRegion ; NetWebView2Lib UDF - #INTERNAL_USE_ONLY#

#Region ; NetWebView2Lib UDF - === EVENT HANDLERS ===
; #INTERNAL_USE_ONLY# ===========================================================================================================
; Name ..........: __NetWebView2_COMErrFunc
; Description ...:
; Syntax ........: __NetWebView2_COMErrFunc($oError)
; Parameters ....: $oError              - an object.
; Return values .: None
; Author ........: mLipok
; Modified ......:
; Remarks .......:
; Related .......:
; Link ..........:
; Example .......: No
; ===============================================================================================================================
Func __NetWebView2_COMErrFunc($oError) ; COM Error Function used by COM Error Handler
	If @Compiled Then Return
	ConsoleWrite("NetWebView2Lib UDF (" & $oError.scriptline & ") : ==> COM Error intercepted !" & @CRLF & _
			@TAB & "err.number is: " & @TAB & @TAB & "0x" & Hex($oError.number) & @CRLF & _
			@TAB & "err.windescription:" & @TAB & $oError.windescription & @CRLF & _
			@TAB & "err.description is: " & @TAB & $oError.description & @CRLF & _
			@TAB & "err.source is: " & @TAB & @TAB & $oError.source & @CRLF & _
			@TAB & "err.helpfile is: " & @TAB & $oError.helpfile & @CRLF & _
			@TAB & "err.helpcontext is: " & @TAB & $oError.helpcontext & @CRLF & _
			@TAB & "err.lastdllerror is: " & @TAB & $oError.lastdllerror & @CRLF & _
			@TAB & "err.scriptline is: " & @TAB & $oError.scriptline & @CRLF & _
			@TAB & "err.retcode is: " & @TAB & "0x" & Hex($oError.retcode) & @CRLF & @CRLF)
EndFunc   ;==>__NetWebView2_COMErrFunc

; Handles native WebView2 events
#TODO => Func __NetWebView2_WebViewEvents__OnMessageReceived(ByRef $oWebV2M, $hGUI, $sMsg)
Func __NetWebView2_WebViewEvents__OnMessageReceived($sMsg)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError
	Local $iSplitPos = StringInStr($sMsg, "|")
	Local $sCommand = $iSplitPos ? StringStripWS(StringLeft($sMsg, $iSplitPos - 1), 3) : $sMsg
	Local $sData = $iSplitPos ? StringTrimLeft($sMsg, $iSplitPos) : ""
	Local $aParts

	Local $s_Prefix = "[WebViewEvents]:" & $sCommand & ": "
	Switch $sCommand
		Case "INIT_READY"
			__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & $sData)

			#QUESTION Why here ExecuteScript ?
			$_g_oWeb.ExecuteScript('window.chrome.webview.postMessage(JSON.stringify({ "type": "COM_TEST", "status": "OK" }));')

		Case "NAV_STARTING"
			__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & $sData)

		Case "NAV_COMPLETED"
			__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & " ?? " & $sData)

		Case "TITLE_CHANGED"
			__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & $sData)
			; If you want to change the title of your GUI based on the page
;~ 			If $aParts[0] > 1 Then WinSetTitle($hGUI, "", "WebView2 - " & $aParts[2])

		Case "WINDOW_RESIZED"
			$aParts = StringSplit($sData, "|")
			If $aParts[0] >= 2 Then
				Local $iW = Int($aParts[1]), $iH = Int($aParts[2])
				; Filter minor resize glitches
				If $iW > 50 And $iH > 50 Then __NetWebView2_Log(@ScriptLineNumber, $s_Prefix & $iW & "x" & $iH)
			EndIf
		Case Else
			__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & (StringLen($sMsg) > 150 ? StringLeft($sMsg, 150) & "..." : $sMsg), 0)
	EndSwitch
	If $_g_sUser_FnPrefix__NetWebView2_WebViewEvents Then
		#TODO =>>>> Call($_g_sUser_FnPrefix__NetWebView2_WebViewEvents & 'OnMessageReceived', $oWebV2M, $hGUI, $sMsg)
		Call($_g_sUser_FnPrefix__NetWebView2_WebViewEvents & 'OnMessageReceived', $sMsg)
	EndIf

EndFunc   ;==>__NetWebView2_WebViewEvents__OnMessageReceived

; Handles custom messages from JavaScript (window.chrome.webview.postMessage)
#TODO => Func __NetWebView2_JSEvents__OnMessageReceived(ByRef $oWebV2M, ByRef $oWebJS, $hGUI, $sMsg)
Func __NetWebView2_JSEvents__OnMessageReceived($sMsg)
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError

	__NetWebView2_Log(@ScriptLineNumber, ">>> [JavaScriptEvents]: " & (StringLen($sMsg) > 150 ? StringLeft($sMsg, 150) & "..." : $sMsg), 0)
	Local $sFirstChar = StringLeft($sMsg, 1)

	; 1. Modern JSON Messaging
	If $sFirstChar = "{" Or $sFirstChar = "[" Then
		__NetWebView2_Log(@ScriptLineNumber, "+> : Processing JSON message..." & @CRLF)
		Local $oJSON = _NetJson_CreateParser()
		If Not IsObj($oJSON) Then Return ConsoleWrite("!> Error: Failed to create NetJson object." & @CRLF)

		$oJSON.Parse($sMsg)
		Local $sJobType = $oJSON.GetTokenValue("type")

		Switch $sJobType
			Case "COM_TEST"
				__NetWebView2_Log(@ScriptLineNumber, "- COM_TEST Confirmed: " & $oJSON.GetTokenValue("status") & @CRLF)
		EndSwitch

	Else
		; 2. Legacy / Native Pipe-Delimited Messaging
		__NetWebView2_Log(@ScriptLineNumber, "+> [JavaScriptEvents]: Processing Delimited message...", 0)
		Local $sCommand, $sData, $iSplitPos
		$iSplitPos = StringInStr($sMsg, "|") - 1

		If $iSplitPos < 0 Then
			$sCommand = StringStripWS($sMsg, 3)
			$sData = ""
		Else
			$sCommand = StringStripWS(StringLeft($sMsg, $iSplitPos), 3)
			$sData = StringTrimLeft($sMsg, $iSplitPos + 1)
		EndIf

		Local $s_Prefix = "[JavaScriptEvents]:" & $sCommand & ": "
		Switch $sCommand
			Case "ERROR"
				__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & " Status General ERROR: " & $sData)

			Case "NAV_ERROR"
				__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & " Status NAV_ERROR: " & $sData)

			Case "COM_TEST"
				__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & " Status Legacy COM_TEST: " & $sData)

			Case "JSON_CLICKED"
				Local $aClickData = StringSplit($sData, "=", 2) ; Split "Key = Value"
				If UBound($aClickData) >= 2 Then
					Local $sKey = StringStripWS($aClickData[0], 3)
					Local $sVal = StringStripWS($aClickData[1], 3)
					__NetWebView2_Log(@ScriptLineNumber, $s_Prefix & " Property: " & $sKey & " | Value: " & $sVal)
				EndIf

		EndSwitch
	EndIf

	If $_g_sUser_FnPrefix__NetWebView2_JSEvents Then
		#TODO =>>>> Call($_g_sUser_FnPrefix__NetWebView2_JSEvents & 'OnMessageReceived', $oWebV2M, $oWebJS, $hGUI, $sMsg)
		Call($_g_sUser_FnPrefix__NetWebView2_JSEvents & 'OnMessageReceived', $sMsg)
	EndIf

EndFunc   ;==>__NetWebView2_JSEvents__OnMessageReceived

#TODO => Func __NetWebView2_WebViewEvents__OnContextMenuRequested(ByRef $oWebV2M, $sLink, $iX, $iY, $sSelection)
Func __NetWebView2_WebViewEvents__OnContextMenuRequested($sLink, $iX, $iY, $sSelection)
	#forceref $sLink, $iX, $iY, $sSelection
	Local $oMyError = ObjEvent("AutoIt.Error", __NetWebView2_COMErrFunc) ; Local COM Error Handler
	#forceref $oMyError
EndFunc   ;==>__NetWebView2_WebViewEvents__OnContextMenuRequested
#EndRegion ; NetWebView2Lib UDF - === EVENT HANDLERS ===
